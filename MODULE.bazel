## MODULE.bazel
module(
    name = "drake-ur-driver",
    version = "0.0.1",
)

# If you don't want to support building on macOS, you may remove the next line.
# Note that it must appear prior to loading "rules_cc", per the documentation:
#  https://github.com/bazelbuild/apple_support?tab=readme-ov-file#bazel-7-setup
bazel_dep(name = "apple_support", version = "1.17.1", repo_name = "build_bazel_apple_support")

# Add the Bazel rules we need.
bazel_dep(name = "rules_cc", version = "0.1.1")
bazel_dep(name = "rules_python", version = "1.0.0")
bazel_dep(name = "bazel_skylib", version = "1.7.1")

drake_dep_repositories = use_extension(
    "@drake//tools/workspace:default.bzl",
    "drake_dep_repositories",
)

use_repo(
    drake_dep_repositories,
    "gflags",
    "lcm",
)

# Here we introduce Drake as a module dependency, but note that Drake is not
# published to any Bazel registry. Below, we'll override it with a github
# source archive.
bazel_dep(name = "drake")

# You can also use DRAKE_COMMIT to choose a Drake release; e.g.:
DRAKE_COMMIT = "v1.42.0"
DRAKE_CHECKSUM = "d860c15f50397c8a946fcc79e0a58a91ebc56f2189ef9edfcac929aa04157f8b"

# Before changing the COMMIT, temporarily uncomment the next line so that Bazel
# displays the suggested new value for the CHECKSUM.
# DRAKE_CHECKSUM = "0" * 64

# This declares the `@drake` module as a source code archive from github.
# See README.md for instructions to use a local path, instead.
archive_override(
    module_name = "drake",
    urls = [x.format(DRAKE_COMMIT) for x in [
        "https://github.com/RobotLocomotion/drake/archive/{}.tar.gz",
    ]],
    sha256 = DRAKE_CHECKSUM,
    strip_prefix = "drake-{}".format(DRAKE_COMMIT.lstrip("v")),
)

new_local_repository = use_repo_rule("@bazel_tools//tools/build_defs/repo:local.bzl", "new_local_repository")
new_local_repository(
    name = "liburclient",
    build_file = "//tools/workspace/Universal_Robots_Client_Library:package.BUILD.bazel",
    path = "Universal_Robots_Client_Library",
)

# Use the host system /usr/bin/python3.
python_repository = use_repo_rule(
    "@drake//tools/workspace/python:repository.bzl",
    "python_repository",
)

python_repository(
    name = "python",
    linux_interpreter_path = "/usr/bin/python3",
    requirements_flavor = "build",
)

register_toolchains("@python//:all")
